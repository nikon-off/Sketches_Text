Функция uralmicroPOST(Запрос)

	ТекущаяДатаСеанса = ТекущаяДатаСеанса(); 
	ТелоКакСтрока = Запрос.ПолучитьТелоКакСтроку();
	
	Ответ = Новый HTTPСервисОтвет(200);
	ЗаписьОтвет = Новый ЗаписьJSON();
	ЗаписьОтвет.УстановитьСтроку();	
	СтруктураОтвета = Новый Структура();
	СтруктураОтвета.Вставить("result", "ok");	
	ЗаписатьJSON(ЗаписьОтвет, СтруктураОтвета);	
	Ответ.УстановитьТелоИзСтроки(ЗаписьОтвет.Закрыть());
	
	ВидАТС = Перечисления.ВидыАТС.UIS;
	
	Попытка	
		
		ТелоЗапроса = РаскодироватьСтроку(ТелоКакСтрока, СпособКодированияСтроки.КодировкаURL);  
		ЗаписьЖурнала = РегистрыСведений.ЖурналОбменаAPI.ДобавитьЗапросВЖурнал("АТС Уралмикро", ТекущаяДатаСеанса, ТелоЗапроса);
		ПараметрыТела = ИнтеграцияАТССервер.ПолучитьПараметрыИзСтроки(ТелоЗапроса, "&");	
		
		УстановитьПривилегированныйРежим(Истина);	
		
		Если НЕ ИнтеграцияАТССервер.АТСТокенБазыДействителен(ПараметрыТела.crm_token, ВидАТС) Тогда
			
			Ответ = СообщениеОбОшибке(400, ЗаписьЖурнала, "Некорректный ключ");
			Возврат Ответ;
			
		КонецЕсли;
		
		ОбязательныеПараметры = "cmd";
		Если НЕ ЕстьОбязательныеПараметры(ПараметрыТела, ОбязательныеПараметры) Тогда
			
			Ответ = СообщениеОбОшибке(400, ЗаписьЖурнала, СтрШаблон("Отсутствуют обязательные параметры: %1", ОбязательныеПараметры));
			Возврат Ответ;
			
		КонецЕсли;   
		
		ТипОперации = ПараметрыТела.cmd; 
		
		Если ТипОперации = "event" Тогда
			
			// Облачная	АТС	отправляет	в	вашу	CRM	уведомления	о	событиях	входящих	звонков	пользователям:
			// появлении,	принятии	или	завершении звонка.	Команда	может	быть	использована	для	
			// отображения	всплывающей	карточки	клиента	в	интерфейсе	CRM.			
			// Имеет смысл подрубать, если реализован сервер взаимодействия
			
			#Область event	
			
			ОбязательныеПараметры = "type,callid,phone";
			Если НЕ ЕстьОбязательныеПараметры(ПараметрыТела, ОбязательныеПараметры) Тогда
				
				Ответ = СообщениеОбОшибке(400, ЗаписьЖурнала, СтрШаблон("Отсутствует обязательные параметры: %1", ОбязательныеПараметры));
				Возврат Ответ;
				
			КонецЕсли;   
			
			ИнтеграцияАТССервер.ОбработатьСобытиеЗвонка(ВидАТС, ПараметрыТела, ЗаписьЖурнала);
			
			#КонецОбласти
			
		ИначеЕсли ТипОперации = "history" Тогда 
			// После успешного звонка в CRM отправляется запрос	с данными о звонке и ссылкой на запись разговора.
			
			#Область history
			
			ОбязательныеПараметры = "callid";
			Если НЕ ЕстьОбязательныеПараметры(ПараметрыТела, ОбязательныеПараметры) Тогда
				
				Ответ = СообщениеОбОшибке(400, ЗаписьЖурнала, СтрШаблон("Отсутствует обязательные параметры: %1", ОбязательныеПараметры));
				Возврат Ответ;
				
			КонецЕсли;   
			
			ИнтеграцияАТССервер.ОбработатьЗавершениеЗвонка(ВидАТС, ПараметрыТела, ЗаписьЖурнала);
			
			#КонецОбласти 
			
		ИначеЕсли ТипОперации = "contact" Тогда 
			
			Возврат Ответ;
			
		Иначе
			
			Ответ = СообщениеОбОшибке(501, ЗаписьЖурнала); // Not implemented
			Возврат Ответ;
			
		КонецЕсли;
		
	Исключение
		
		ЗаписьЖурнала = РегистрыСведений.ЖурналОбменаAPI.ДобавитьЗапросВЖурнал("АТС Уралмикро", ТекущаяДатаСеанса, ТелоКакСтрока); 	
		
		Ответ = Новый HTTPСервисОтвет(500);
		
		ЗаписьОтвет = Новый ЗаписьJSON();		
		ЗаписьОтвет.УстановитьСтроку();
		
		СтруктураОтвета = Новый Структура();
		СтруктураОтвета.Вставить("result", "error");
		СтруктураОтвета.Вставить("message", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписатьJSON(ЗаписьОтвет, СтруктураОтвета);	
		
		Ответ.УстановитьТелоИзСтроки(ЗаписьОтвет.Закрыть());
		Ответ.Заголовки.Вставить("Content-Type", "charset=utf-8");
		
		РегистрыСведений.ЖурналОбменаAPI.ДобавитьОтветВЖурнал(ЗаписьЖурнала, ТекущаяДатаСеанса, 500, Ответ.ПолучитьТелоКакСтроку());		
		
		Возврат Ответ;
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции
